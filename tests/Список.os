#Использовать ".."

&Тест
Процедура ИзЭлементов() Экспорт
	
	// Когда

	Результат = Список.ИзЭлементов(
		1, 2, 3, 4, 5, 6, 7, 8, 9, 9
	);

	// Тогда

	Ожидаем.Что(Результат)
		.ИмеетТип("ФиксированныйСписок")
		.ИмеетДлину(10);

	Для Сч = 1 По 9 Цикл

		Ожидаем.Что(Результат.Содержит(Сч)).ЭтоИстина();

	КонецЦикла;

КонецПроцедуры

&Тест
Процедура Скопировать() Экспорт

	// Дано

	СписокМассив = Новый СписокМассив;
	СписокМассив.Добавить(1);
	СписокМассив.Добавить(2);

	// Когда

	Результат = Список.Скопировать(СписокМассив);

	// Тогда

	Ожидаем.Что(Результат)
		.ИмеетТип("ФиксированныйСписок")
		.ИмеетДлину(2);

	Ожидаем.Что(Результат.Содержит(1)).ЭтоИстина();
	Ожидаем.Что(Результат.Содержит(2)).ЭтоИстина();

КонецПроцедуры

&Тест
Процедура СкопироватьВозвращаетСамуКоллекциюЕслиЭтоФиксированныйСписок() Экспорт

	// Дано
	
	КопируемыйСписок = Список.ИзЭлементов(
		1, 2, 3, 4, 5, 6, 7, 8, 9, 9
	);

	// Когда

	Результат = Список.Скопировать(КопируемыйСписок);

	// Тогда

	Ожидаем.Что(Результат).Равно(КопируемыйСписок);

КонецПроцедуры