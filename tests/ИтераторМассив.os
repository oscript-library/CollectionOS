#Использовать asserts
#Использовать ".."
#Использовать "./ТестМодуль"

Перем ДляКаждогоВыполнилсяРаз; // Количество раз которое выполнился метод ДляКаждого

&Тест
Процедура ИтераторСоздается() Экспорт

	// Когда
	Результат = Новый ИтераторМассив(Новый Массив, ЭтотОбъект);

	// Тогда

	Ожидаем.Что(Результат).ИмеетТип("ИтераторМассив");

КонецПроцедуры

&Тест
Процедура ЕстьСледующий() Экспорт
	
	// Дано
	
	Коллекция = Новый Массив;
	Коллекция.Добавить(1);

	Итератор = Новый ИтераторМассив(Коллекция, ЭтотОбъект);

	// Когда

	Результат = Итератор.ЕстьСледующий();

	// Тогда

	Ожидаем.Что(Результат).ЭтоИстина();

КонецПроцедуры

&Тест
Процедура НетСледующего() Экспорт
	
	// Дано
	
	Итератор = Новый ИтераторМассив(Новый Массив, ЭтотОбъект);

	// Когда

	Результат = Итератор.ЕстьСледующий();

	// Тогда

	Ожидаем.Что(Результат).ЭтоЛожь();

КонецПроцедуры

&Тест
Процедура Следующий() Экспорт
	
	// Дано
	
	Коллекция = Новый Массив;
	Коллекция.Добавить(1);

	Итератор = Новый ИтераторМассив(Коллекция, ЭтотОбъект);

	// Когда

	Результат = Итератор.Следующий();

	// Тогда

	Ожидаем.Что(Результат).Равно(1);

КонецПроцедуры

&Тест
Процедура СледующийВызываетИсключениеПриКонкурентнойМодификации() Экспорт
	
	// Дано
	
	Массив = Новый Массив;
	Массив.Добавить(1);

	Итератор = Новый ИтераторМассив(Массив, ЭтотОбъект);
	ВызватьСобытие("КоллекцияМодифицирована", Новый Массив);

	// Тогда

	Ожидаем.Что(Итератор)
		.Метод("Следующий")
		.ВыбрасываетИсключение("Коллекция была изменена в процессе обхода");

КонецПроцедуры

&Тест
Процедура СледующийВызываетИсключениеПриВыходеЗаГраницы() Экспорт
	
	// Дано
	
	Итератор = Новый ИтераторМассив(Новый Массив, ЭтотОбъект);

	// Тогда

	Ожидаем.Что(Итератор)
		.Метод("Следующий")
		.ВыбрасываетИсключение("Значение индекса выходит за пределы диапазона");

КонецПроцедуры

&Тест
Процедура Удалить() Экспорт
	
	// Дано
	
	Коллекция = Новый Массив;
	Коллекция.Добавить(1);

	Итератор = Новый ИтераторМассив(Коллекция, ЭтотОбъект);
	Итератор.Следующий();

	// Когда

	Итератор.Удалить(); 

	// Тогда

	Ожидаем.Что(Коллекция).ИмеетДлину(0);

КонецПроцедуры

&Тест
Процедура УдалитьВызываетИсключениеПриКонкурентнойМодификации() Экспорт
	
	// Дано
	
	Массив = Новый Массив;
	Массив.Добавить(1);

	Итератор = Новый ИтераторМассив(Массив, ЭтотОбъект);
	Итератор.Следующий();

	// Когда
	
	ВызватьСобытие("КоллекцияМодифицирована", Новый Массив);

	// Тогда

	Ожидаем.Что(Итератор)
		.Метод("Удалить")
		.ВыбрасываетИсключение("Коллекция была изменена в процессе обхода");

КонецПроцедуры

&Тест
Процедура УдалитьВызываетИсключениеПриНеустановленномЭлементе() Экспорт
	
	// Дано
	
	Итератор = Новый ИтераторМассив(Новый Массив, ЭтотОбъект);

	// Тогда

	Ожидаем.Что(Итератор)
		.Метод("Удалить")
		.ВыбрасываетИсключение("Итератор не указывает на элемент коллекции");

КонецПроцедуры

&Тест
Процедура ДляКаждогоОставшегося() Экспорт
	
	// Дано
	
	ДляКаждогоВыполнилсяРаз = 0;

	Коллекция = Новый Массив;
	Коллекция.Добавить(0);
	Коллекция.Добавить(1);
	Коллекция.Добавить(2);

	Итератор = Новый ИтераторМассив(Коллекция, ЭтотОбъект);
	
	Итератор.Следующий();

	// Когда

	Итератор.ДляКаждогоОставшегося(Новый Действие(ЭтотОбъект, "ДействиеДляКаждогоОставшегося"));

	// Тогда

	Ожидаем.Что(ДляКаждогоВыполнилсяРаз).Равно(2);
	Ожидаем.Что(Итератор.ЕстьСледующий()).ЭтоЛожь();

КонецПроцедуры

&Тест
Процедура ДляКаждогоОставшегосяЛямбда() Экспорт
	
	// Дано
	
	ТестМодуль.ДляКаждогоВыполнилсяРаз = 0;
	ТестМодуль.СуммаЭлементов          = 0;

	Коллекция = Новый Массив;
	Коллекция.Добавить(0);
	Коллекция.Добавить(1);
	Коллекция.Добавить(2);

	Итератор = Новый ИтераторМассив(Коллекция, ЭтотОбъект);
	
	Итератор.Следующий();

	// Когда

	Итератор.ДляКаждогоОставшегося(
		"Элемент -> ТестМодуль.ДляКаждогоВыполнилсяРаз = ТестМодуль.ДляКаждогоВыполнилсяРаз + 1;
		|	ТестМодуль.СуммаЭлементов = ТестМодуль.СуммаЭлементов + Элемент;"
	);

	// Тогда

	Ожидаем.Что(ТестМодуль.ДляКаждогоВыполнилсяРаз).Равно(2);
	Ожидаем.Что(ТестМодуль.СуммаЭлементов).Равно(3);
	Ожидаем.Что(Итератор.ЕстьСледующий()).ЭтоЛожь();

КонецПроцедуры

&Тест
Процедура ПриМутацииИтераторомИнвалидируютсяДругиеИтераторы() Экспорт
	
	// Дано
	
	ДляКаждогоВыполнилсяРаз = 0;

	Коллекция = Новый Массив;
	Коллекция.Добавить(0);
	Коллекция.Добавить(1);
	Коллекция.Добавить(2);

	ИтераторМутирующий       = Новый ИтераторМассив(Коллекция, ЭтотОбъект);
	ИтераторИнвалидирующийся = Новый ИтераторМассив(Коллекция, ЭтотОбъект);
	
	// Когда
	
	ИтераторМутирующий.Следующий();
	ИтераторМутирующий.Удалить();

	// Тогда
	
	Ожидаем.Что(ИтераторМутирующий.Следующий()).Заполнено();
	Ожидаем.Что(ИтераторИнвалидирующийся)
		.Метод("Следующий")
		.ВыбрасываетИсключение("Коллекция была изменена в процессе обхода");

КонецПроцедуры

Процедура ДействиеДляКаждогоОставшегося(Элемент) Экспорт
	
	ДляКаждогоВыполнилсяРаз = ДляКаждогоВыполнилсяРаз + 1;

	Ожидаем.Что(Элемент).Равно(ДляКаждогоВыполнилсяРаз);

КонецПроцедуры
