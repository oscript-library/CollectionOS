#Использовать reflector

#Использовать ".."

&Тест
Процедура ПроверитьРеализациюИнтерфейсов() Экспорт
	
	КартаИнтерфейсов = Новый Соответствие;

	Интерфейсы = НайтиФайлы(
		ОбъединитьПути("src", "internal", "Классы", "Интерфейсы"),
		"*.os",
		Истина
	);
	
	Для Каждого Интерфейс Из Интерфейсы Цикл
		
		Аннотации = Новый РефлекторОбъекта(Тип(Интерфейс.ИмяБезРасширения))
			.ПолучитьТаблицуМетодов("Интерфейс", Ложь)
			[0].Аннотации;
		
		Наследует = Новый Массив;
		
		Для Каждого Аннотация Из Аннотации Цикл
			
			Если НРег(Аннотация.Имя) = НРег("Наследует") Тогда
				Наследует.Добавить(Аннотация.Параметры[0].Значение);
			КонецЕсли;
			
		КонецЦикла;
		
		КартаИнтерфейсов.Вставить(Интерфейс.ИмяБезРасширения, Наследует);
		
	КонецЦикла;
	
	ПутиКоллекций = Новый Массив;
	ПутиКоллекций.Добавить(ОбъединитьПути("src", "Классы"));
	ПутиКоллекций.Добавить(ОбъединитьПути("src", "internal", "Классы", "Коллекции"));
	ПутиКоллекций.Добавить(ОбъединитьПути("src", "internal", "Классы", "Итераторы"));
	
	ФайлыКоллекции = Новый Массив;
	
	Для Каждого Путь Из ПутиКоллекций Цикл
		
		Для Каждого Файл Из НайтиФайлы(Путь, "*.os", Истина) Цикл
			ФайлыКоллекции.Добавить(Файл);
		КонецЦикла;
		
	КонецЦикла;
	
	Отказ = Ложь;
	
	Для Каждого ФайлКоллекция Из ФайлыКоллекции Цикл
		
		РефлекторОбъекта = Новый РефлекторОбъекта(Тип(ФайлКоллекция.ИмяБезРасширения));
		
		Аннотации = РефлекторОбъекта.ПолучитьТаблицуМетодов("Реализует", Ложь)[0].Аннотации;
		
		РеализуемыеИнтерфейсы = Новый Массив;
		
		Для Каждого Аннотация Из Аннотации Цикл
			
			Если НРег(Аннотация.Имя) = НРег("Реализует") Тогда
				РеализуемыеИнтерфейсы.Добавить(Аннотация.Параметры[0].Значение);
			КонецЕсли;
			
		КонецЦикла;
		
		Методы = РефлекторОбъекта.ПолучитьТаблицуМетодов( , Истина);
		
		Для Каждого РеализуемыйИнтерфейс Из РеализуемыеИнтерфейсы Цикл
			
			ПроверитьРеализациюИнтерфейсаРекурсивно(
				КартаИнтерфейсов,
				РеализуемыйИнтерфейс,
				Методы,
				ФайлКоллекция.ИмяБезРасширения,
				Отказ
			);
			
		КонецЦикла;
		
	КонецЦикла;
	
	Если Отказ Тогда
		ВызватьИсключение "Не все коллекции реализуют их интерфейсы";
	КонецЕсли;
	
КонецПроцедуры

Процедура ПроверитьРеализациюИнтерфейсаРекурсивно(КартаИнтерфейсов, РеализуемыйИнтерфейс, Методы, ИмяКласса, Отказ)
	
	Интерфейс = Новый ИнтерфейсОбъекта;
	Интерфейс.ИзОбъекта(Тип(РеализуемыйИнтерфейс));
	
	Если НЕ Интерфейс.Реализован(Методы, Истина) Тогда
		
		Сообщить(
			СтрШаблон(
				"Класс <%1> не реализует интерфейс <%2>",
				ИмяКласса,
				РеализуемыйИнтерфейс
			),
			СтатусСообщения.ОченьВажное
		);
		
		Отказ = Истина;
		
	КонецЕсли;
	
	Для Каждого Интерфейс Из КартаИнтерфейсов.Получить(РеализуемыйИнтерфейс) Цикл
		
		ПроверитьРеализациюИнтерфейсаРекурсивно(
			КартаИнтерфейсов,
			Интерфейс,
			Методы,
			ИмяКласса,
			Отказ
		);
		
	КонецЦикла;
	
КонецПроцедуры