#Использовать asserts

#Использовать ".."

Перем ДляКаждогоВыполнилсяРаз; // Количество раз которое выполнился метод ДляКаждого

&Тест
Процедура ФиксированноеМножествоКлючейИЗначенийСоздаётся() Экспорт

	// Дано

	// Когда

	Результат = Новый ФиксированноеМножествоКлючейИЗначений(
		Новый Соответствие,
		ЭтотОбъект
	);

	// Тогда

	Ожидаем.Что(Результат).ИмеетТип("ФиксированноеМножествоКлючейИЗначений");

КонецПроцедуры

&Тест
Процедура Итератор() Экспорт

	// Дано

	ПроверяемаяКоллекция = Новый ФиксированноеМножествоКлючейИЗначений(
		Новый Соответствие,
		ЭтотОбъект
	);

	// Когда

	Результат = ПроверяемаяКоллекция.Итератор();

	// Тогда

	Ожидаем.Что(Результат).ИмеетТип("ФиксированныйИтератор");

КонецПроцедуры

&Тест
Процедура ДляКаждого() Экспорт

	// Дано

	ДляКаждогоВыполнилсяРаз = 0;

	ПроверяемаяКоллекция = Новый ФиксированноеМножествоКлючейИЗначений(
		Соответствие(),
		ЭтотОбъект
	);

	// Когда

	ПроверяемаяКоллекция.ДляКаждого(
		Новый Действие(ЭтотОбъект, "ДействиеДляКаждого")
	);

	// Тогда

	Ожидаем.Что(ДляКаждогоВыполнилсяРаз).Равно(3);

КонецПроцедуры

&Тест
Процедура Содержит() Экспорт

	// Дано
	
	ПроверяемаяКоллекция = Новый ФиксированноеМножествоКлючейИЗначений(
		Соответствие(),
		ЭтотОбъект
	);

	Соответствие = Новый Соответствие;
	Соответствие.Вставить("3", 3);

	// Когда

	Для каждого КлючИЗначение Из Соответствие Цикл

		Результат = ПроверяемаяКоллекция.Содержит(КлючИЗначение);

	КонецЦикла;

	// Тогда

	Ожидаем.Что(Результат).ЭтоИстина();

КонецПроцедуры

&Тест
Процедура НеСодержит() Экспорт

	// Дано
	
	ПроверяемаяКоллекция = Новый ФиксированноеМножествоКлючейИЗначений(
		Соответствие(),
		ЭтотОбъект
	);

	Соответствие = Новый Соответствие;
	Соответствие.Вставить("4", 4);

	// Когда

	Для каждого КлючИЗначение Из Соответствие Цикл

		Результат = ПроверяемаяКоллекция.Содержит(КлючИЗначение);

	КонецЦикла;

	// Тогда

	Ожидаем.Что(Результат).ЭтоЛожь();

КонецПроцедуры

&Тест
Процедура СодержитВсе() Экспорт

	// Дано
	
	ПроверяемаяКоллекция = Новый ФиксированноеМножествоКлючейИЗначений(
		Соответствие(),
		ЭтотОбъект
	);

	ПроверяющаяКоллекция = Новый ФиксированноеМножествоКлючейИЗначений(
		Соответствие(),
		ЭтотОбъект
	);

	// Когда

	Результат = ПроверяемаяКоллекция.СодержитВсе(ПроверяющаяКоллекция);

	// Тогда

	Ожидаем.Что(Результат).ЭтоИстина();

КонецПроцедуры

&Тест
Процедура НеСодержитВсе() Экспорт

	// Дано
	
	ПроверяемаяКоллекция = Новый ФиксированноеМножествоКлючейИЗначений(
		Соответствие(),
		ЭтотОбъект
	);

	Соответствие = Соответствие();
	Соответствие.Вставить("4", 4);

	ПроверяющаяКоллекция = Новый ФиксированноеМножествоКлючейИЗначений(
		Соответствие,
		ЭтотОбъект
	);

	// Когда

	Результат = ПроверяемаяКоллекция.СодержитВсе(ПроверяющаяКоллекция);

	// Тогда

	Ожидаем.Что(Результат).ЭтоЛожь();

КонецПроцедуры

&Тест
Процедура НеПусто() Экспорт

	// Дано

	ПроверяемаяКоллекция = Новый ФиксированноеМножествоКлючейИЗначений(
		Соответствие(),
		ЭтотОбъект
	);

	// Когда

	Результат = ПроверяемаяКоллекция.Пусто();

	// Тогда

	Ожидаем.Что(Результат).ЭтоЛожь();

КонецПроцедуры

&Тест
Процедура Пусто() Экспорт

	// Дано

	ПроверяемаяКоллекция = Новый ФиксированноеМножествоКлючейИЗначений(
		Новый Соответствие,
		ЭтотОбъект
	);

	// Когда

	Результат = ПроверяемаяКоллекция.Пусто();

	// Тогда

	Ожидаем.Что(Результат).ЭтоИстина();

КонецПроцедуры

&Тест
Процедура ПроцессорКоллекции() Экспорт

	// Дано

	ПроверяемаяКоллекция = Новый ФиксированноеМножествоКлючейИЗначений(
		Соответствие(),
		ЭтотОбъект
	);

	// Когда

	Результат = ПроверяемаяКоллекция.ПроцессорКоллекции();

	// Тогда

	Ожидаем.Что(Результат).ИмеетТип("ПроцессорКоллекций");

КонецПроцедуры

&Тест
Процедура Количество() Экспорт

	// Дано

	ПроверяемаяКоллекция = Новый ФиксированноеМножествоКлючейИЗначений(
		Соответствие(),
		ЭтотОбъект
	);

	// Когда

	Результат = ПроверяемаяКоллекция.Количество();

	// Тогда

	Ожидаем.Что(Результат).Равно(3);

КонецПроцедуры

&Тест
Процедура ВМассив() Экспорт

	// Дано

	Соответствие = Соответствие();

	ПроверяемаяКоллекция = Новый ФиксированноеМножествоКлючейИЗначений(
		Соответствие,
		ЭтотОбъект
	);

	// Когда

	Результат = ПроверяемаяКоллекция.ВМассив();

	// Тогда

	Ожидаем.Что(Результат)
		.ИмеетТип("Массив")
		.ИмеетДлину(3);

	Для каждого Элемент Из Результат Цикл
		Ожидаем.Что(Соответствие.Получить(Элемент.Ключ))
			.Равно(Элемент.Значение);
	КонецЦикла;

КонецПроцедуры

&Тест
Процедура ПреставлениеПереопределяется() Экспорт
	
	// Дано

	ПроверяемаяКоллекция = Новый ФиксированноеМножествоКлючейИЗначений(
		Соответствие(),
		ЭтотОбъект
	);
	
	// Когда

	Результат = Строка(ПроверяемаяКоллекция);

	// Тогда

	Ожидаем.Что(Результат)
		.Равно("[1=1, 2=2, 3=3]");

КонецПроцедуры

Процедура ДействиеДляКаждого(Элемент) Экспорт // BSLLS:UnusedParameters-off
	ДляКаждогоВыполнилсяРаз = ДляКаждогоВыполнилсяРаз + 1;	
КонецПроцедуры

Функция Соответствие()
	
	Результат = Новый Соответствие;

	Результат.Вставить("1", 1);
	Результат.Вставить("2", 2);
	Результат.Вставить("3", 3);

	Возврат Результат;

КонецФункции