#Использовать asserts
#Использовать decorator

#Использовать ".."

Перем Рефлектор;               // Рефлектор
Перем ДляКаждогоВыполнилсяРаз; // Количество раз которое выполнился метод ДляКаждого
Перем МножествоФикстура;       // Кеш множества фикстуры

&Тест
Процедура ФиксированноеМножествоСоответствиеСоздается() Экспорт

	// Когда

	Результат = Новый ФиксированноеМножествоСоответствие;

	// Тогда

	Ожидаем.Что(Результат).ИмеетТип("ФиксированноеМножествоСоответствие");

КонецПроцедуры

&Тест
Процедура ФиксированноеМножествоСоответствиеСоздаетсяКопированием() Экспорт

	// Дано
	
	КопируемоеМножество = МножествоФикстура();

	// Когда

	Результат = Новый ФиксированноеМножествоСоответствие(КопируемоеМножество);

	// Тогда
	
	Ожидаем.Что(Результат).ИмеетТип("ФиксированноеМножествоСоответствие");

КонецПроцедуры

&Тест
Процедура Итератор() Экспорт
	
	// Дано

	ФиксированноеМножество = Новый ФиксированноеМножествоСоответствие;
	Рефлектор.УстановитьСвойство(
		ФиксированноеМножество,
		"ВнутреннееМножество",
		МножествоФикстура()
	);

	// Когда

	Результат = ФиксированноеМножество.Итератор();

	// Тогда

	Ожидаем.Что(Результат).ИмеетТип("ЧитающийИтераторКлючСоответствие");

КонецПроцедуры

&Тест
Процедура ДляКаждого() Экспорт
	
	// Дано

	ДляКаждогоВыполнилсяРаз = 0;

	ФиксированноеМножество = Новый ФиксированноеМножествоСоответствие;
	Рефлектор.УстановитьСвойство(
		ФиксированноеМножество,
		"ВнутреннееМножество",
		МножествоФикстура()
	);

	// Когда

	ФиксированноеМножество.ДляКаждого(Новый Действие(ЭтотОбъект, "ДействиеДляКаждого"));

	// Тогда

	Ожидаем.Что(ДляКаждогоВыполнилсяРаз).Равно(3);

КонецПроцедуры

&Тест
Процедура Содержит() Экспорт
	
	// Дано

	ФиксированноеМножество = Новый ФиксированноеМножествоСоответствие;
	Рефлектор.УстановитьСвойство(
		ФиксированноеМножество,
		"ВнутреннееМножество",
		МножествоФикстура()
	);

	// Когда

	Результат = ФиксированноеМножество.Содержит(1);

	// Тогда

	Ожидаем.Что(Результат).ЭтоИстина();

КонецПроцедуры

&Тест
Процедура СодержитВсе() Экспорт
	
	// Дано

	ФиксированноеМножество = Новый ФиксированноеМножествоСоответствие;
	Рефлектор.УстановитьСвойство(
		ФиксированноеМножество,
		"ВнутреннееМножество",
		МножествоФикстура()
	);

	// Когда

	Результат = ФиксированноеМножество.СодержитВсе(Новый Массив);

	// Тогда

	Ожидаем.Что(Результат).ЭтоИстина();

КонецПроцедуры

&Тест
Процедура Пусто() Экспорт
	
	// Дано

	ФиксированноеМножество = Новый ФиксированноеМножествоСоответствие;
	Рефлектор.УстановитьСвойство(
		ФиксированноеМножество,
		"ВнутреннееМножество",
		МножествоФикстура()
	);

	// Когда

	Результат = ФиксированноеМножество.Пусто();

	// Тогда

	Ожидаем.Что(Результат).ЭтоЛожь();

КонецПроцедуры

&Тест
Процедура ПроцессорКоллекции() Экспорт
	
	// Дано

	ФиксированноеМножество = Новый ФиксированноеМножествоСоответствие;
	Рефлектор.УстановитьСвойство(
		ФиксированноеМножество,
		"ВнутреннееМножество",
		МножествоФикстура()
	);

	// Когда

	Результат = ФиксированноеМножество.ПроцессорКоллекции();

	// Тогда

	Ожидаем.Что(Результат).ИмеетТип("ПроцессорКоллекций");

КонецПроцедуры

&Тест
Процедура Количество() Экспорт
	
	// Дано

	ФиксированноеМножество = Новый ФиксированноеМножествоСоответствие;
	Рефлектор.УстановитьСвойство(
		ФиксированноеМножество,
		"ВнутреннееМножество",
		МножествоФикстура()
	);

	// Когда

	Результат = ФиксированноеМножество.Количество();

	// Тогда

	Ожидаем.Что(Результат).Равно(3);

КонецПроцедуры

&Тест
Процедура ВМассив() Экспорт
	
	// Дано

	ФиксированноеМножество = Новый ФиксированноеМножествоСоответствие;
	Рефлектор.УстановитьСвойство(
		ФиксированноеМножество,
		"ВнутреннееМножество",
		МножествоФикстура()
	);

	// Когда

	Результат = ФиксированноеМножество.ВМассив();

	// Тогда

	Ожидаем.Что(Результат)
		.ИмеетТип("Массив")
		.ИмеетДлину(3)
		.Содержит(1)
		.Содержит(2)
		.Содержит(3);

КонецПроцедуры

Процедура ДействиеДляКаждого(Элемент) Экспорт // BSLLS:UnusedParameters-off
	ДляКаждогоВыполнилсяРаз = ДляКаждогоВыполнилсяРаз + 1;	
КонецПроцедуры

Функция МножествоФикстура()
	
	Если МножествоФикстура <> Неопределено Тогда
		Возврат МножествоФикстура;
	КонецЕсли;

	Коллекция = Новый Соответствие;
	Коллекция.Вставить(1, Истина);
	Коллекция.Вставить(2, Истина);
	Коллекция.Вставить(3, Истина);

	Кеш = Новый Массив();

	Для каждого КиЗ Из Коллекция Цикл
		Кеш.Добавить(КиЗ);
	КонецЦикла;

	ПолеСоответствие = Новый Поле("Соответствие")
		.Публичное()
		.ЗначениеПоУмолчанию(Коллекция);

	МетодИтератор = Новый Метод("Итератор")
		.Публичный()
		.ТелоМетода("Возврат Новый ИтераторСоответствие(Соответствие, ЭтотОбъект);");

	МетодДляКаждого = Новый Метод("ДляКаждого")
		.Публичный()
		.Параметр(Новый ПараметрМетода("Алгоритм"))
		.ТелоМетода("
		|	Для каждого Элемент Из Соответствие Цикл
		|		Алгоритм.Выполнить(Элемент.Ключ);
		|	КонецЦикла;
		|");

	МетодСодержит = Новый Метод("Содержит")
		.Публичный()
		.Параметр(Новый ПараметрМетода("Элемент"))
		.ТелоМетода("Возврат Истина;");

	МетодСодержитВсе = Новый Метод("СодержитВсе")
		.Публичный()
		.Параметр(Новый ПараметрМетода("Коллекция"))
		.ТелоМетода("Возврат Истина;");

	МетодПусто = Новый Метод("Пусто")
		.Публичный()
		.ТелоМетода("Возврат Ложь;");

	МетодПроцессорКоллекции = Новый Метод("ПроцессорКоллекции")
		.Публичный()
		.ТелоМетода("Возврат ПроцессорыКоллекций.ИзКоллекции(Новый Массив);");

	МетодКоличество = Новый Метод("Количество")
		.Публичный()
		.ТелоМетода("Возврат 3;");

	МетодВМассив = Новый Метод("ВМассив")
		.Публичный()
		.ТелоМетода(
			"Результат = Новый Массив;
			|
			|Результат.Добавить(1);
			|Результат.Добавить(2);
			|Результат.Добавить(3);
			|
			|Возврат Результат;"
		);

	МножествоФикстура = Новый ПостроительДекоратора()
		.Поле(ПолеСоответствие)
		.Метод(МетодИтератор)
		.Метод(МетодДляКаждого)
		.Метод(МетодСодержит)
		.Метод(МетодСодержитВсе)
		.Метод(МетодПусто)
		.Метод(МетодПроцессорКоллекции)
		.Метод(МетодКоличество)
		.Метод(МетодВМассив)
		.Построить();

	Возврат МножествоФикстура;

КонецФункции

Рефлектор = Новый Рефлектор();
