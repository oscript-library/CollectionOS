#Использовать asserts
#Использовать ".."

Перем ДляКаждогоВыполнилсяРаз; // Количество раз которое выполнился метод ДляКаждого

&Тест
Процедура ИтераторСоздается() Экспорт

	// Когда
	Результат = Новый ИтераторСоответствие(Новый Соответствие, ЭтотОбъект);

	// Тогда

	Ожидаем.Что(Результат).ИмеетТип("ИтераторСоответствие");

КонецПроцедуры

&Тест
Процедура ЕстьСледующий() Экспорт
	
	// Дано
	
	Коллекция = Новый Соответствие;
	Коллекция.Вставить(1, 1);

	Итератор = Новый ИтераторСоответствие(Коллекция, ЭтотОбъект);

	// Когда

	Результат = Итератор.ЕстьСледующий();

	// Тогда

	Ожидаем.Что(Результат).ЭтоИстина();

КонецПроцедуры

&Тест
Процедура НетСледующего() Экспорт
	
	// Дано
	
	Итератор = Новый ИтераторСоответствие(Новый Соответствие, ЭтотОбъект);

	// Когда

	Результат = Итератор.ЕстьСледующий();

	// Тогда

	Ожидаем.Что(Результат).ЭтоЛожь();

КонецПроцедуры

&Тест
Процедура Следующий() Экспорт
	
	// Дано
	
	Коллекция = Новый Соответствие;
	Коллекция.Вставить(1, 1);

	Итератор = Новый ИтераторСоответствие(Коллекция, ЭтотОбъект);

	// Когда

	Результат = Итератор.Следующий();

	// Тогда

	Ожидаем.Что(Результат).ИмеетТип("КлючИЗначение");
	Ожидаем.Что(Результат.Ключ).Равно(1);
	Ожидаем.Что(Результат.Значение).Равно(1);

КонецПроцедуры

&Тест
Процедура СледующийВызываетИсключениеПриКонкурентнойМодификации() Экспорт
	
	// Дано
	
	Соответствие = Новый Соответствие;
	Соответствие.Вставить(1, 1);

	Итератор = Новый ИтераторСоответствие(Соответствие, ЭтотОбъект);
	ВызватьСобытие("КоллекцияМодифицирована", Новый Массив);

	// Тогда

	Ожидаем.Что(Итератор)
		.Метод("Следующий")
		.ВыбрасываетИсключение("Коллекция была изменена в процессе обхода");

КонецПроцедуры

&Тест
Процедура СледующийВызываетИсключениеПриВыходеЗаГраницы() Экспорт
	
	// Дано
	
	Итератор = Новый ИтераторСоответствие(Новый Соответствие, ЭтотОбъект);

	// Тогда

	Ожидаем.Что(Итератор)
		.Метод("Следующий")
		.ВыбрасываетИсключение("Значение индекса выходит за пределы диапазона");

КонецПроцедуры

&Тест
Процедура Удалить() Экспорт
	
	// Дано
	
	Коллекция = Новый Соответствие;
	Коллекция.Вставить(1, 1);

	Итератор = Новый ИтераторСоответствие(Коллекция, ЭтотОбъект);
	Итератор.Следующий();

	// Когда

	Итератор.Удалить(); 

	// Тогда

	Ожидаем.Что(Коллекция).ИмеетДлину(0);

КонецПроцедуры

&Тест
Процедура УдалитьВызываетИсключениеПриКонкурентнойМодификации() Экспорт
	
	// Дано
	
	Соответствие = Новый Соответствие;
	Соответствие.Вставить(1);

	Итератор = Новый ИтераторСоответствие(Соответствие, ЭтотОбъект);
	Итератор.Следующий();

	// Когда
	
	ВызватьСобытие("КоллекцияМодифицирована", Новый Массив);

	// Тогда

	Ожидаем.Что(Итератор)
		.Метод("Удалить")
		.ВыбрасываетИсключение("Коллекция была изменена в процессе обхода");

КонецПроцедуры

&Тест
Процедура УдалитьВызываетИсключениеПриНеустановленномЭлементе() Экспорт
	
	// Дано
	
	Итератор = Новый ИтераторСоответствие(Новый Соответствие, ЭтотОбъект);

	// Тогда

	Ожидаем.Что(Итератор)
		.Метод("Удалить")
		.ВыбрасываетИсключение("Итератор не указывает на элемент коллекции");

КонецПроцедуры

&Тест
Процедура ДляКаждогоОставшегося() Экспорт
	
	// Дано
	
	ДляКаждогоВыполнилсяРаз = 0;

	Коллекция = Новый Соответствие;
	Коллекция.Вставить(0, 0);
	Коллекция.Вставить(1, 1);
	Коллекция.Вставить(2, 2);

	Итератор = Новый ИтераторСоответствие(Коллекция, ЭтотОбъект);
	
	Итератор.Следующий();

	// Когда

	Итератор.ДляКаждогоОставшегося(Новый Действие(ЭтотОбъект, "ДействиеДляКаждогоОставшегося"));

	// Тогда

	Ожидаем.Что(ДляКаждогоВыполнилсяРаз).Равно(2);
	Ожидаем.Что(Итератор.ЕстьСледующий()).ЭтоЛожь();

КонецПроцедуры

&Тест
Процедура ПриМутацииИтераторомИнвалидируютсяДругиеИтераторы() Экспорт
	
	// Дано
	
	ДляКаждогоВыполнилсяРаз = 0;

	Коллекция = Новый Соответствие;
	Коллекция.Вставить(0, 0);
	Коллекция.Вставить(1, 1);
	Коллекция.Вставить(2, 2);

	ИтераторМутирующий       = Новый ИтераторСоответствие(Коллекция, ЭтотОбъект);
	ИтераторИнвалидирующийся = Новый ИтераторСоответствие(Коллекция, ЭтотОбъект);
	
	// Когда
	
	ИтераторМутирующий.Следующий();
	ИтераторМутирующий.Удалить();

	// Тогда

	Ожидаем.Что(ИтераторМутирующий.Следующий()).Заполнено();
	Ожидаем.Что(ИтераторИнвалидирующийся)
		.Метод("Следующий")
		.ВыбрасываетИсключение("Коллекция была изменена в процессе обхода");

КонецПроцедуры

Процедура ДействиеДляКаждогоОставшегося(Элемент) Экспорт
	
	ДляКаждогоВыполнилсяРаз = ДляКаждогоВыполнилсяРаз + 1;

	Ожидаем.Что(Элемент.Ключ).Равно(ДляКаждогоВыполнилсяРаз);

КонецПроцедуры
