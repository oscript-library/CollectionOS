#Использовать ".."

&Тест
Процедура ИзЭлементов() Экспорт
	
	// Когда

	Результат = Множество.ИзЭлементов(
		1, 2, 3, 4, 5, 6, 7, 8, 9, 9
	);

	// Тогда

	Ожидаем.Что(Результат)
		.ИмеетТип("ФиксированноеМножество")
		.ИмеетДлину(9);

	Для Сч = 1 По 9 Цикл

		Ожидаем.Что(Результат.Содержит(Сч)).ЭтоИстина();

	КонецЦикла;

КонецПроцедуры

&Тест
Процедура Скопировать() Экспорт

	// Дано

	МножествоСоответствие = Новый МножествоСоответствие;
	МножествоСоответствие.Добавить(1);
	МножествоСоответствие.Добавить(2);

	// Когда

	Результат = Множество.Скопировать(МножествоСоответствие);

	// Тогда

	Ожидаем.Что(Результат)
		.ИмеетТип("ФиксированноеМножество")
		.ИмеетДлину(2);

	Ожидаем.Что(Результат.Содержит(1)).ЭтоИстина();
	Ожидаем.Что(Результат.Содержит(2)).ЭтоИстина();

КонецПроцедуры

&Тест
Процедура СкопироватьВозвращаетСамуКоллекциюЕслиЭтоФиксированноеМножество() Экспорт

	// Дано
	
	КопируемоеМножество = Множество.ИзЭлементов(
		1, 2, 3, 4, 5, 6, 7, 8, 9, 9
	);

	// Когда

	Результат = Множество.Скопировать(КопируемоеМножество);

	// Тогда

	Ожидаем.Что(Результат).Равно(КопируемоеМножество);

КонецПроцедуры