#Использовать fluent

Перем Соответствие; // Соответствие карты
Перем Источник;     // Карта владелец этого представления

Функция Итератор() Экспорт

	Возврат Новый ФиксированныйИтератор(
		Новый ИтераторКлючСоответствие(Соответствие, Источник)
	);

КонецФункции

Процедура ДляКаждого(Алгоритм, Контекст = Неопределено) Экспорт
	Итератор().ДляКаждогоОставшегося(Алгоритм, Контекст);
КонецПроцедуры

Функция Содержит(Элемент) Экспорт
	Возврат Соответствие.Получить(Элемент) <> Неопределено;
КонецФункции

Функция СодержитВсе(Коллекция) Экспорт

	Результат = Истина;

	Итератор = Коллекция.Итератор();

	Пока Итератор.ЕстьСледующий() Цикл

		Если Не Содержит(Итератор.Следующий()) Тогда
			Результат = Ложь;
			Прервать;
		КонецЕсли;

	КонецЦикла;

	Возврат Результат;

КонецФункции

Функция Пусто() Экспорт
	Возврат Количество() = 0;
КонецФункции

Функция ПроцессорКоллекции() Экспорт
	Возврат ПроцессорыКоллекций.ИзКоллекции(ВМассив());
КонецФункции

Функция Количество() Экспорт
	Возврат Соответствие.Количество();
КонецФункции

Функция ВМассив() Экспорт
	
	Результат = Новый Массив;
	Итератор  = Итератор();
	
	Пока Итератор.ЕстьСледующий() Цикл
		Результат.Добавить(Итератор.Следующий());
	КонецЦикла;

	Возврат Результат;

КонецФункции

Процедура ОбработкаПолученияПредставления(Представление, СтандартнаяОбработка) // BSLLS:UnusedLocalMethod-off
	КоллекцииСлужебный.ОбработчикПолученияПредставленияКоллекции(ЭтотОбъект, Представление, СтандартнаяОбработка);
КонецПроцедуры

Функция ПолучитьИтератор() // BSLLS:UnusedLocalMethod-off
	Возврат Новый СлужебныйИтераторДляДвижка(Итератор());
КонецФункции

&Обходимое
&Реализует("ЧитаемоеМножество")
Процедура ПриСозданииОбъекта(пСоответствие, пИсточник)

	Соответствие = пСоответствие;
	Источник     = пИсточник;

КонецПроцедуры
