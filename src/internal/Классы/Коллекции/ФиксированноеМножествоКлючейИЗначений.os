#Использовать fluent

Перем Соответствие; // Соответствие карты
Перем Источник;     // Карта владелец этого представления

Функция Итератор() Экспорт

	Возврат Новый ФиксированныйИтератор(
		Новый ИтераторСоответствие(Соответствие, Источник)
	);

КонецФункции

Процедура ДляКаждого(Алгоритм) Экспорт
	Итератор().ДляКаждогоОставшегося(Алгоритм);
КонецПроцедуры

Функция Содержит(Элемент) Экспорт

	Значение = Соответствие.Получить(Элемент.Ключ);

	Возврат Значение <> Неопределено И Значение = Элемент.Значение;

КонецФункции

Функция СодержитВсе(Коллекция) Экспорт

	Результат = Истина;

	Итератор = Коллекция.Итератор();

	Пока Итератор.ЕстьСледующий() Цикл

		Если Не Содержит(Итератор.Следующий()) Тогда
			Результат = Ложь;
			Прервать;
		КонецЕсли;

	КонецЦикла;

	Возврат Результат;

КонецФункции

Функция Пусто() Экспорт
	Возврат Количество() = 0;
КонецФункции

Функция ПроцессорКоллекции() Экспорт
	Возврат ПроцессорыКоллекций.ИзКоллекции(Соответствие);
КонецФункции

Функция Количество() Экспорт
	Возврат Соответствие.Количество();
КонецФункции

Функция ВМассив() Экспорт

	Результат = Новый Массив;
	Итератор  = Итератор();
	
	Пока Итератор.ЕстьСледующий() Цикл
		Результат.Добавить(Итератор.Следующий());
	КонецЦикла;

	Возврат Результат;

КонецФункции

Процедура ОбработкаПолученияПредставления(Представление, СтандартнаяОбработка) // BSLLS:UnusedLocalMethod-off
	
	КартыСлужебный.ОбработчикПолученияПредставленияМножестваКлючейИЗначений(
		ЭтотОбъект,
		Представление,
		СтандартнаяОбработка
	);

КонецПроцедуры

&Реализует("ЧитаемоеМножество")
Процедура ПриСозданииОбъекта(пСоответствие, пИсточник)

	Соответствие = пСоответствие;
	Источник     = пИсточник;

КонецПроцедуры
