Перем Соответствие;       // Итерируемое соответствие
Перем ВнутреннийИтератор; // Ссылка на итератор по массиву пар КлючИЗначение
Перем ТекущийЭлемент;     // Текущий элемент на который  указывает итератор

Функция ЕстьСледующий() Экспорт
	Возврат ВнутреннийИтератор.ЕстьСледующий();
КонецФункции

Функция Следующий() Экспорт

	ТекущийЭлемент = ВнутреннийИтератор.Следующий();
	
	Возврат ТекущийЭлемент;

КонецФункции

Процедура Удалить() Экспорт
	
	ВнутреннийИтератор.Удалить();

	Соответствие.Удалить(
		ТекущийЭлемент.Ключ
	);

КонецПроцедуры

Процедура ДляКаждогоОставшегося(Действие, Контекст = Неопределено) Экспорт
	
	ВнутреннийИтератор.ДляКаждогоОставшегося(Действие, Контекст);

КонецПроцедуры

&Реализует("Итератор")
Процедура ПриСозданииОбъекта(пСоответствие, Источник)

	Соответствие = пСоответствие;
	
	МассивУзлов  = Новый Массив;

	Для Каждого КлючИЗначение Из Соответствие Цикл
		МассивУзлов.Добавить(КлючИЗначение);
	КонецЦикла;

	ВнутреннийИтератор = Новый ИтераторМассив(МассивУзлов, Источник);
	
КонецПроцедуры
