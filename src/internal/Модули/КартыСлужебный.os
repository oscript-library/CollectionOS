
// Стандартный обработчик получения представления для множества от ключей и значений
//
// Параметры:
//  Множество            - ЧитаемоеМножество<КлючИЗначение> - Коллекция, представление которой будет рассчитано
//  Представление        - Строка - Представление множества
//  СтандартнаяОбработка - Булево - Признак стандартной обработки получения представления
//
Процедура ОбработчикПолученияПредставленияМножестваКлючейИЗначений(
	Множество, Представление, СтандартнаяОбработка) Экспорт	

	СтандартнаяОбработка = Ложь;

	Представление = СтрШаблон(
		"[%1]",
		ПредставлениеМножестваКлючейИЗначений(Множество, Множество)		
	);

КонецПроцедуры

// Стандартный обработчик получения представления для всех карт
//
// Параметры:
//  Карта                - ЧитаемаяКарта - Карта, представление которой будет рассчитано
//  Представление        - Строка - Представление коллекции
//  СтандартнаяОбработка - Булево - Признак стандартной обработки получения представления
//
Процедура ОбработчикПолученияПредставленияКарты(
	Карта, Представление, СтандартнаяОбработка) Экспорт

	СтандартнаяОбработка = Ложь;

	Представление = СтрШаблон(
		"{%1}",
		ПредставлениеМножестваКлючейИЗначений(Карта.КлючиИЗначения(), Карта)
	);

КонецПроцедуры

Функция ПредставлениеМножестваКлючейИЗначений(Множество, Владелец)
	
	Массив = Новый Массив;

	Итератор = Множество.Итератор();

	Пока Итератор.ЕстьСледующий() Цикл

		Элемент = Итератор.Следующий();

		Массив.Добавить(
			СтрШаблон(
				"%1=%2",
				?(Элемент.Ключ = Владелец, "(Эта карта)", Элемент.Ключ),
				?(Элемент.Значение = Владелец, "(Эта карта)", Элемент.Значение)
			)
		);

	КонецЦикла;

	Возврат СтрСоединить(Массив, ", ");

КонецФункции
