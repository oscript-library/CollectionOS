Функция КакКарта(Соответствие) Экспорт
	
	Результат = Новый КартаСоответствие();

	Для каждого КлючИЗначение Из Соответствие Цикл
		Результат.Вставить(КлючИЗначение.Ключ, КлючИЗначение.Значение);
	КонецЦикла;

	Возврат Результат;

КонецФункции

Функция ИзЭлементов(	
	// BSLLS:NumberOfParams-off
	// BSLLS:NumberOfOptionalParams-off
	// BSLLS:UnusedParameters-off
		Ключ1 = NULL, Значение1 = NULL, Ключ2 = NULL, Значение2 = NULL,
		Ключ3 = NULL, Значение3 = NULL, Ключ4 = NULL, Значение4 = NULL,
		Ключ5 = NULL, Значение5 = NULL, Ключ6 = NULL, Значение6 = NULL,
		Ключ7 = NULL, Значение7 = NULL, Ключ8 = NULL, Значение8 = NULL,
		Ключ9 = NULL, Значение9 = NULL, Ключ10 = NULL, Значение10 = NULL
	// BSLLS:NumberOfParams-on
	// BSLLS:NumberOfOptionalParams-on
	// BSLLS:UnusedParameters-on
	) Экспорт

	Результат = Новый Соответствие;

	Для сч = 1 По 10 Цикл

		Ключ     = Вычислить("Ключ" + сч);
		Значение = Вычислить("Значение" + сч);

		Если Ключ = NULL Или Значение = NULL Тогда
			Прервать;
		КонецЕсли;

		Результат.Вставить(Ключ, Значение);

	КонецЦикла;

	Возврат Новый ФиксированноеСоответствие(Результат);

КонецФункции

Функция Скопировать(Соответствие) Экспорт
	Возврат Новый Соответствие(Новый ФиксированноеСоответствие(Соответствие));
КонецФункции

